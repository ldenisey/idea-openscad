plugins {
    id 'org.jetbrains.intellij' version '1.8.0'
    id 'org.jetbrains.changelog' version '1.3.1'
    id 'com.github.ldenisey.setversions' version '1.0.1'
}

apply plugin: 'org.jetbrains.changelog'

group = 'com.javampire'
version = '2.2.1-SNAPSHOT'

sourceCompatibility = 1.11
targetCompatibility = 1.11
sourceSets.main.java.srcDirs = ['src/main/java', 'src/main/gen']

ext {
    pluginVerifierIdeVersions =
            "IIC-2020.1.3, " +
            "IIC-2022.2, " +
            "PCC-2020.1.3, " +
            "PCC-2022.2, " +
            "CL-2020.1, " +
            "CL-2022.2, " +
            "PS-2020.1, " +
            "PS-2022.2, " +
            "WS-2020.1," +
            "WS-2022.2"
}

repositories {
    mavenCentral()
}

intellij {
    version.set("221-EAP-SNAPSHOT")

    plugins.set([
            'java'
            //,'psiviewer:221-SNAPSHOT'  // For debugging the parser only
    ])

    updateSinceUntilBuild.set(false)

    patchPluginXml {
        sinceBuild.set('201.6668')
        untilBuild.set('')

        changeNotes.set(provider { changelog.getLatest().toHTML() })
    }
}

// Read more: https://github.com/JetBrains/gradle-changelog-plugin
changelog {
    version.set(project.version)
    header.set(project.version)
    headerParserRegex.set(~/\d\.\d\.\d(-SNAPSHOT)?/)
    unreleasedTerm.set("Unreleased")
}

runPluginVerifier {
    ideVersions = (List) pluginVerifierIdeVersions.split(',')
}

publishPlugin {
    token = System.getenv("intellijPublishToken")
}
